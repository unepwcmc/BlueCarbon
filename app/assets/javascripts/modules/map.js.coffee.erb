window.Map = class Map
  HABITATS =
    mangrove: '#d21010'
    seagrass: '#d00eeb'
    saltmarsh: '#007dff'
    algal_mat: '#ffe048'
    other: '#1dcbea'
  DEFAULT_MAP_OPTS =
    attributionControl: false
    center: [25, 53.7]
    zoom: 8
    minZoom: 8
    maxZoom: 19

  constructor: (elementId, mapOpts={}) ->
    @baseMap = L.tileLayer('http://tile.cloudmade.com/BC9A493B41014CAABB98F0471D759707/997/256/{z}/{x}/{y}.png', {maxZoom: 17})
    @baseSatellite =  new L.tileLayer("https://api.mapbox.com/v4/unepwcmc.d8952968/{z}/{x}/{y}.png?access_token=pk.eyJ1IjoidW5lcHdjbWMiLCJhIjoiRXg1RERWRSJ9.taTsSWwtAfFX_HMVGo2Cug")

    @initializeMap(elementId, mapOpts)
    @addAttribution()
    @addOverlays( (err, overlays) =>
      L.control.layers(@baseMaps, overlays).addTo(@map)
    )

  initializeMap: (elementId, mapOpts) ->
    mapOpts = _.extend(DEFAULT_MAP_OPTS, mapOpts)
    mapOpts.layers = [@baseSatellite]
    @map = L.map(elementId, mapOpts)

  addAttribution: ->
    attribution = L.control.attribution(position: 'bottomright', prefix: '')
    attribution.addAttribution('Developed for the UAE Blue Carbon Demonstration Project')
    attribution.addTo(@map)
    attribution = L.control.attribution(position: 'bottomright', prefix: '')
    attribution.addAttribution("© <a href='https://www.mapbox.com/map-feedback/'>Mapbox</a> © <a href='http://www.openstreetmap.org/copyright'>OpenStreetMap contributors</a>")
    attribution.addTo(@map)

  baseMaps: ->
    baseMaps = {}
    baseMaps[polyglot.t('analysis.map')] = @baseMap
    baseMaps[polyglot.t('analysis.satellite')] = @baseSatellite

  addOverlays: (done) ->
    async.reduce(@getSublayers(), {}, (sublayers, sublayer, cb) =>
      cartodb.Tiles.getTiles({
        sublayers: [sublayer.cartodb]
        type: 'cartodb'
        user_name: "carbon-tool"
      }, (tiles, error) =>
        prettyName = polyglot.t("analysis.#{sublayer.habitat}")
        sublayers[prettyName] = L.tileLayer(tiles.tiles[0]).addTo(@map)
        cb(null, sublayers)
      )
    )

  getSublayers: ->
    _.map(HABITATS, (polygon_fill, habitat) ->
      tableName = "bc_#{habitat}<%= "_#{Rails.env}" unless Rails.env.production? %>"

      query = "SELECT * FROM #{tableName} WHERE toggle = true AND (action <> 'delete' OR action IS NULL)"
      style = """
        ##{tableName} {
          line-color: #FFF;
          line-width: 0.5;
          polygon-fill: #{polygon_fill};
          polygon-opacity: 0.6
        }
      """

      {habitat: habitat, cartodb: {sql: query, cartocss: style}}
    )
